apply plugin: 'com.android.library'

android {
    compileSdkVersion 27

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 27
        versionCode 1
        versionName "1.0"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.android.support:appcompat-v7:27.1.1'
    implementation 'com.android.support:design:27.1.1'
    implementation 'com.android.support.constraint:constraint-layout:1.1.0'
}

task copyLibs(type: Copy, dependsOn: build) {
    group 'build'
    from configurations.compile
    into 'libs'
}

task clearLibraryAar(type: Delete, dependsOn: copyLibs) {
    delete fileTree("${deployAarPath}") {
        include 'oneconnect-**.aar'
    }
}

task deployLibraryAar(type: Copy, dependsOn: clearLibraryAar) {
    group 'build'
    from('build/outputs/aar/')
    into("${deployAarPath}")
    include('oneconnect-release.aar')
    rename('oneconnect-release.aar', 'oneconnect.aar')
    doLast {
        fileTree("${deployAarPath}") {
            include { it.file.name ==~ "^oneconnect-([0-9.]+).aar.meta\$" }
        }.each { f -> f.renameTo(file("${deployAarPath}/oneconnect-.aar.meta")) }
    }
}

tasks.whenTaskAdded { task ->
    if (task.name == 'bundleRelease') {
        task.finalizedBy 'deployLibraryAar'
    }
}
